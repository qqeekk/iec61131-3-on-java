Package javast.sablecc;


Helpers

  sign =
    '+' | '-';

  dash =
    '-';

  underscore =
    '_';

  bit =
    '0' | '1';

  digit =
    '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9';

  octal_digit =
    '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7';

  hex_digit =
    '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9' | 'a' | 'b' | 'c' | 'd' | 'e' | 'f' | 'A' | 'B' | 'C' | 'D' | 'E' | 'F';

  unicode_input_character =
    [0..0xffff];

  letter =
    [0x0041..0x005a] |
    [0x0061..0x007a]; // English alphabet

  letter_or_digit =
    letter |
    digit;

  underscored_letter_or_digit =
    ('_')? letter_or_digit;

  common_character_representation =
    [unicode_input_character - ['$' + [''' + '"']]] |
    '$$' |
    '$L' | '$N' | '$P' | '$R' | '$T' |
    '$l' | '$n' | '$p' | '$r' | '$t';

  underscored_digit =
    ('_')? digit;

  exponent_sign =
    'E' | 'e';

  integer =
    digit underscored_digit*;

  exponent =
    exponent_sign sign? integer;

  underscored_bit =
    ('_')? bit;

  binary_integer =
    '2#' bit underscored_bit*;

  underscored_octal_digit =
    ('_')? octal_digit;

  octal_integer =
    '8#' octal_digit underscored_octal_digit*;

  underscored_hex_digit =
    ('_')? hex_digit;

  hex_integer =
    '16#' hex_digit underscored_hex_digit*;

  dollar =
    '$';

  squote =
    ''';

  dquote =
    '"';

  location_prefix =
    'I' | 'Q' | 'M';

  size_prefix =
    'X' | 'B' | 'W' | 'D' | 'L';

  real_type_name =
    'REAL' | 'LREAL';

  signed_integer_type_name =
    'SINT' | 'INT' | 'DINT' | 'LINT';

  unsigned_integer_type_name =
    'USINT' | 'UINT' | 'UDINT' | 'ULINT';

  bit_string_literal_type_name =
    'BYTE' | 'WORD' | 'DWORD' | 'LWORD';

  bool_type_name =
    'BOOL';

  time_of_day_type_name_full =
    'TIME_OF_DAY' | 'TOD';

  date_type_name_full =
    'DATE' | 'D';

  date_and_time_type_name_full =
    'DATE_AND_TIME' | 'DT';

  time_type_name_full =
    'T' | 'TIME';

  day_identifier =
    'd';

  hour_identifier =
    'h';

  minute_identifier =
    'm';

  second_identifier =
    's';

  millisecond_identifier =
    'ms';

  integer_type_name =
    signed_integer_type_name |
    unsigned_integer_type_name;

  signed_integer_literal =
    sign? integer |
    binary_integer |
    octal_integer |
    hex_integer;

  unsigned_integer_literal =
    integer |
    binary_integer |
    octal_integer |
    hex_integer;

  fixed_point =
    integer ('.' integer)?;

  milliseconds =
    fixed_point millisecond_identifier;

  seconds =
    fixed_point second_identifier |
    integer second_identifier underscore? milliseconds;

  minutes =
    fixed_point minute_identifier  |
    integer minute_identifier underscore? seconds;

  hours =
    fixed_point hour_identifier |
    integer hour_identifier underscore? minutes;

  days =
    fixed_point day_identifier |
    integer day_identifier underscore? hours;

  interval =
    days |
    hours |
    minutes |
    seconds |
    milliseconds;

  day_hour =
    integer;

  day_minute =
    integer;

  day_second =
    fixed_point;

  daytime =
    day_hour ':' day_minute ':' day_second;

  year =
    integer;

  month =
    integer;

  day =
    integer;

  date_literal =
    year '-' month '-' day;


  single_byte_character_representation =
    common_character_representation |
    dollar squote |
    dquote |
    dollar hex_digit hex_digit;

  double_byte_character_representation =
    common_character_representation |
    dollar dquote |
    squote |
    dollar hex_digit hex_digit hex_digit hex_digit;

  ht  = 0x0009;
  lf  = 0x000a;
  ff  = 0x000c;
  cr  = 0x000d;
  sp  = ' ';
  line_terminator = lf | cr | cr lf;

  white_space = (sp | ht | ff | line_terminator);


Tokens

  jit_token = '(**' sp? 'JIT' sp? '**)';

  comment = '(*' [unicode_input_character - '*']* '*)';

  sign =
    '+' | '-';

  dollar =
    '$';

  dot =
    '.';

  colon =
    ':';

  semicolon =
    ';';

  colon_eq =
    ':=';

  comma =
    ',';

  lparen =
    '(';

  rparen =
    ')';

  lbrack =
    '[';

  rbrack =
    ']';

  or_token =
    'OR';

  xor_token =
    'XOR';

  and_token_full =
    '&' | 'AND';

  eq_token_full =
    '=' | '<>';

  comparison_operator =
    '<' | '>' | '<=' | '>=';

  mul_operator =
    '*' | '/' | 'MOD';

  unary_operator =
    '-' | 'NOT';

  not_token =
    'NOT';

  power_token =
    '**';

  return_token =
    'RETURN';

  arrow_token =
    '=>';

  // B.1.1 Letters, digits and identifiers

  single_byte_character_string =
    squote single_byte_character_representation* squote;

  double_byte_character_string =
    dquote double_byte_character_representation* dquote;

  // B.1.2.1 Numeric literals

  string1_type_name =
    'STRING';

  wstring_type_name =
    'WSTRING';

  time_type_name =
    'TIME';

  real_type_name =
    real_type_name;

  integer_type_name =
    integer_type_name;

  date_type_name =
    'DATE' | 'TIME_OF_DAY' | 'TOD' | 'DATE_AND_TIME' | 'DT';

  bit_string_type_name =
    'BOOL' | 'BYTE' | 'WORD' | 'DWORD' | 'LWORD';

  bit_string_literal_type_name =
    bit_string_literal_type_name;

  bool_type_name =
    bool_type_name;

  true_false_literal =
    'TRUE' | 'FALSE';

  // B.1.2.1 Numeric literals

  integer_literal =
    integer_type_name '#' signed_integer_literal |
    unsigned_integer_literal;

  real_literal =
    real_type_name '#' sign? integer '.' integer exponent? |
    integer '.' integer exponent?;

  bit_string_literal =
    (bit_string_literal_type_name '#')? unsigned_integer_literal;

  bool_bit_literal =
    (bool_type_name '#')? bit;

  subrange =
    integer '..' integer;

  // B.1.2.3.1 Duration

  duration =
    time_type_name_full '#' dash? interval;

  // B.1.2.3.2 Time of day and date

  time_of_day =
    time_of_day_type_name_full '#' daytime;

  date =
    date_type_name_full '#' date_literal;

  date_and_time =
    date_and_time_type_name_full '#' date_literal dash daytime;

  type_token =
    'TYPE';

  end_type_token =
    'END_TYPE';

  array_token =
    'ARRAY';

  of_token =
    'OF';

  struct_token =
    'STRUCT';

  end_struct_token =
    'END_STRUCT';

  var_token =
    'VAR';

  end_var_token =
    'END_VAR';

  constant_token =
    'CONSTANT';

  var_input_token =
    'VAR_INPUT';

  var_output_token =
    'VAR_OUTPUT';

  var_in_out_token =
    'VAR_IN_OUT';

  var_external_token =
    'VAR_EXTERNAL';

  var_temp_token =
    'VAR_TEMP';

  var_access_token =
    'VAR_ACCESS';

  function_token =
    'FUNCTION';

  end_function_token =
    'END_FUNCTION';

  function_block_token =
    'FUNCTION_BLOCK';

  end_function_block_token =
    'END_FUNCTION_BLOCK';

  program_token =
    'PROGRAM';

  end_program_token =
    'END_PROGRAM';

  at_token =
    'AT';

  if_token =
    'IF';

  then_token =
    'THEN';

  end_if_token =
    'END_IF';

  else_if_token =
    'ELSIF';

  else_token =
    'ELSE';

  case_token =
    'CASE';

  end_case_token =
    'END_CASE';

  for_token =
    'FOR';

  do_token =
    'DO';

  end_for_token =
    'END_FOR';

  to_token =
    'TO';

  by_token =
    'BY';

  while_token =
    'WHILE';

  end_while_token =
    'END_WHILE';

  repeat_token =
    'REPEAT';

  end_repeat_token =
    'END_REPEAT';

  until_token =
    'UNTIL';

  exit_token =
    'EXIT';

  incompl_location =
    'AT' '%' location_prefix '*';

  edge =
    'R_EDGE' |
    'F_EDGE';

  retain_token =
    'RETAIN';

  non_retain_token =
    'NON_RETAIN';

  retain =
    'RETAIN' |
    'NON_RETAIN';

  standard_function_block_name =
    'SR' | 'RS' | 'R_TRIG' | 'F_TRIG' |
    'CTU' | 'CTD' | 'CTUD' |
    'TP' | 'TON' | 'TOF'; // 2.5.2.3

  direction =
    'READ_WRITE' |
    'READ_ONLY';

  identifier =
    (letter | '_' letter_or_digit) underscored_letter_or_digit*;

  // make a single long white_spaces token
  white_spaces =
    white_space+;

  direct_variable =
    '%' location_prefix size_prefix? integer ('.' integer)*;

Ignored Tokens

  white_spaces, comment;


Productions

  library =
    library_element_declaration *;

  // B.0

  library_element_declaration =
    {data_type} data_type_declaration |
    {function} function_declaration |
    {function_block} function_block_declaration |
    {program} program_declaration;
    //{configuration} configuration_declaration;

  // B.1.2 Constants

  numeric_literal =
    {integer} integer_literal |
    {real} real_literal;

  constant =
    {numeric} sign? numeric_literal |
    {character} character_string |
    {time} time_literal |
    {bit_string} bit_string_literal |
    {boolean} boolean_literal;

  // B.1.2.1 Boolean

  boolean_literal =
    {bool} bool_bit_literal |
    {true_false} true_false_literal;

  // B.1.2.2 Character strings

  character_string =
    {single} single_byte_character_string |
    {double} double_byte_character_string;

  // B.1.2.3 Time literals

  time_literal =
    {duration} duration |
    {time_of_day} time_of_day |
    {date} date |
    {date_and_time} date_and_time;

  // B.1.3 Data types

  non_generic_type_name =
    {elementary} elementary_type_name |
    {derived} derived_type_name;

  // B.1.3.1 Elementary data types

  elementary_type_name =
    {numeric} numeric_type_name |
    {date} date_type_name |
    {bit_string} bit_string_type_name edge? |
    {string} string1_type_name |
    {wstring} wstring_type_name |
    {time} time_type_name;

  numeric_type_name =
    {integer} integer_type_name |
    {real} real_type_name;

  // B.1.3.3 Derived data types

  derived_type_name =
    {array_struct_string_simple} identifier;

  // single_element_type_name =
    //{subrange} subrange_type_name |
    //{enumerated} enumerated_type_name

  simple_type_name =
    identifier;

  array_type_name =
    identifier;

  structure_type_name =
    identifier;

  string_type_name =
    identifier;

  structure_element_name =
    identifier;

  data_type_declaration_element =
    type_declaration semicolon;

  data_type_declaration =
    type_token data_type_declaration_element+ end_type_token;

  type_declaration =
    {single_type} single_element_type_declaration |
    {array_type} array_type_declaration |
    {struct_type} structure_type_declaration |
    {string_type} string_type_declaration;

  array_type_declaration =
    array_type_name colon array_spec_init;

  single_element_type_declaration =
    simple_type_declaration;
    //{subrange} subrange_type_declaration |
    //{enumerated} enumerated_type_declaration

  simple_type_declaration =
    simple_type_name colon simple_spec_init;

  simple_spec_initialization =
    colon_eq constant;

  simple_spec_init =
    simple_specification simple_spec_initialization?;

  simple_specification =
    {elementary} elementary_type_name; // |
    //{simple} simple_type_name; // todo: remove

  subrange_list =
    subrange_list_head* subrange;

  subrange_list_head =
    subrange comma;

  array_spec_initialization =
    colon_eq array_initialization;

  array_spec_init =
    array_specification array_spec_initialization?;

  array_specification =
    //{named} array_type_name | // todo: temporary disabled
    {inline} array_token lbrack subrange_list rbrack of_token non_generic_type_name;

  array_initialization =
    lbrack array_initial_elements_list rbrack;

  array_initial_elements_list =
    array_initial_elements_list_head* array_initial_elements;

  array_initial_elements_list_head =
    array_initial_elements comma;

  array_initial_elements =
    {single} initial_element |
    {repeat} integer_literal lparen initial_element? rparen;

  initial_element =
    {constant} constant |
    //{enumerated} enumerated_value |
    {struct} structure_initialization |
    {array} array_initialization;

  structure_type_declaration =
    structure_type_name colon structure_specification;

  structure_specification =
    {structure_declaration} structure_declaration |
    {initialized} initialized_structure;

  initialized_structure_initialization =
    colon_eq structure_initialization;

  initialized_structure =
    structure_type_name initialized_structure_initialization?;

  structure_declaration =
    struct_token structure_element_declaration_list_head+ end_struct_token;

  structure_element_declaration_list_head =
    structure_element_declaration semicolon;

  structure_element_declaration =
    structure_element_name colon structure_element_init;

  structure_element_init =
    {simple} simple_spec_init |
    //{subrange} subrange_spec_init |
    //{enumerated} enumerated_spec_init |
    {array} array_spec_init |
    {struct} initialized_structure;

  structure_initialization =
    lparen structure_element_initialization_list rparen;

  structure_element_initialization_list =
    structure_element_initialization_list_head* structure_element_initialization;

  structure_element_initialization_list_head =
    structure_element_initialization comma;

  structure_element_initialization =
    structure_element_name colon_eq initial_element;

  string_wstring_type_name =
    {string} string1_type_name |
    {wstring} wstring_type_name;

  string_type_declaration_initialization =
    colon_eq [val]:character_string;

  string_type_declaration =
    // string_type_name colon string_wstring_type_name brack_integer? string_type_declaration_initialization?;
    {len} string_type_name colon string_wstring_type_name brack_integer string_type_declaration_initialization?;
    // | {len_spec} string_type_name colon string_wstring_type_name string_type_declaration_initialization;

  // B.1.4 Variables

  variable =
    {direct_variable} direct_variable |
    {symbolic_variable} symbolic_variable;

  variable_complex =
    {direct_variable} direct_variable |
    {symbolic_variable} symbolic_variable_complex;

  symbolic_variable =
    {variable} identifier |
    {array} symbolic_variable subscript_list |
    {struct} symbolic_variable dot field_selector;

  symbolic_variable_complex =
    {array} symbolic_variable subscript_list |
    {struct} symbolic_variable dot field_selector;

  // B.1.4.2 Multi-element variables

  subscript_list =
    lbrack subscript_list_head* subscript rbrack;

  subscript_list_head =
    subscript comma;

  subscript =
    expression;

  field_selector =
    identifier;

  // B.1.4.3 Declaration and initialization

  input_declarations =
    var_input_token retain? var_init_decl+ end_var_token;

  output_declarations =
    var_output_token retain? var_init_decl+ end_var_token;

  input_output_declarations =
    var_in_out_token var_init_decl+ end_var_token;

  var_declarations =
    var_token constant_token_or_retain? var_init_decl+ end_var_token;

  external_var_declarations =
    var_external_token constant_token? var_init_decl+ end_var_token;

  temp_var_declarations =
    var_temp_token var_init_decl+ end_var_token;

  var_init_decl =
    var1_list incompl_location? colon var_init_decl_spec semicolon;

  var_init_decl_spec =
    {struct_fb_name} identifier initialized_structure_initialization? |
    {var} var1_spec_init |
    {array_var} array_spec_init |
    {s_string} single_byte_string_spec |
    {d_string} double_byte_string_spec;

  var1_list =
    var1_list_head* identifier;

  var1_list_head =
    identifier comma;

  var1_spec_init =
    {simple} simple_spec_init;
    //{subrange} subrange_spec_init |
    //{enumerated} enumerated_spec_init

  constant_token_or_retain =
    {constant} constant_token |
    {retain} retain;

  located_var_declarations =
    var_token constant_token_or_retain? located_var_decl_list end_var_token;

  located_var_decl_list =
    located_var_decl_list_head* located_var_decl;

  located_var_decl_list_head =
    located_var_decl semicolon;

  located_var_decl =
    identifier? location colon located_var_spec_init;

  located_var_spec_init =
    {simple} simple_spec_init |
    //subrange_spec_init |
    //enumerated_spec_init |
    {array} array_spec_init |
    {struct} initialized_structure |
    {sstring} single_byte_string_spec |
    {dstring} double_byte_string_spec;

  location =
    at_token direct_variable;

  brack_integer =
    lbrack integer_literal rbrack;

  single_byte_string_spec_initialization =
    colon_eq single_byte_character_string;

  single_byte_string_spec =
    string1_type_name brack_integer single_byte_string_spec_initialization?;

  double_byte_string_spec_initialization =
    colon_eq double_byte_character_string;

  double_byte_string_spec =
    wstring_type_name brack_integer double_byte_string_spec_initialization?;

  // B.1.5 Program organization units

  // B.1.5.1 Functions

  function_name =
    identifier; // custom + 2.5.1.5

  function_declaration =
    jit_token? function_token function_name colon return_type_name function_var_declaration* function_body end_function_token;

  return_type_name =
    {elementary} elementary_type_name |
    {derived} derived_type_name;

  function_var_declaration =
    {io} io_var_declarations |
    {function} var_declarations;

  io_var_declarations =
    {input} input_declarations |
    {ouput} output_declarations |
    {io} input_output_declarations;

  function_body =
    //ladder_diagram |
    //function_block_diagram |
    //instruction_list |
    statement_list;
    //<other languages>

  // B.1.5.2 Function blocks

  function_block_type_name =
    {standard} standard_function_block_name |
    {derived} derived_function_block_name;

  derived_function_block_name =
    identifier;

  function_block_declaration =
    function_block_token derived_function_block_name function_block_var_declarations* function_block_body end_function_block_token;

  function_block_var_declarations =
    {io} io_var_declarations |
    {other} other_var_declarations;

  other_var_declarations =
    {external} external_var_declarations |
    {var} var_declarations |
    {temp} temp_var_declarations;

  function_block_body =
    statement_list;
    // other langs;

  // B.1.5.3 Programs

  program_declaration =
    program_token identifier program_var_declarations* function_block_body end_program_token;

  program_var_declarations =
    {io} io_var_declarations |
    {other} other_var_declarations |
    {located} located_var_declarations |
    {access} program_access_decls;

  program_access_decls =
    var_access_token program_access_decl_list end_var_token;

  program_access_decl_list =
    program_access_decl_list_head* program_access_decl;

  program_access_decl_list_head =
    program_access_decl semicolon;

  program_access_decl =
    identifier [fst]:colon symbolic_variable [snd]:colon non_generic_type_name direction?;

  // B.3 Language ST (Structured Text)

  // B.3.1 Expressions

  expression =
    {or} expression or_token xor_expression |
    {unary_expression} xor_expression;

  xor_expression =
    {xor} xor_expression xor_token and_expression |
    {unary} and_expression;

  and_expression =
    {and} and_expression and_token_full comparison |
    {unary} comparison;

  comparison =
    {comparison} comparison eq_token_full equ_expression |
    {unary} equ_expression;

  equ_expression =
    {equ} equ_expression comparison_operator add_expression |
    {unary} add_expression;

  add_expression =
    {add} add_expression add_operator term |
    {unary} term;

  add_operator =
    sign;

  term =
    {mul} term mul_operator power_expression |
    {term} power_expression;

  power_expression =
    {power} power_expression power_token unary_expression |
    {unary} unary_expression;

  unary_expression =
    unary_operator* primary_expression;

  primary_expression =
    {constant} constant |
    //enumerated_value |
    {variable} variable |
    {paren} lparen expression rparen |
    {call} function_name lparen param_assignment_list rparen;

  param_assignment_list =
    param_assignment_list_head* param_assignment;

  param_assignment_list_head =
    param_assignment comma;

  // B.3.2 Statements

  statement_list =
    statement_list_head+;

  statement_list_head =
    statement? semicolon;

  // First two result in shift-reduce
  statement =
    {assign} identifier colon_eq expression |
    {call} identifier lparen param_assignment_list? rparen |
    {assign2} variable_complex colon_eq expression |
    {return} return_token |
    {selection} selection_statement |
    {iter} iteration_statement;


  // B.3.2.2 Subprogram control statements

  param_assignment_lvalue =
    identifier colon_eq;

  param_assignment =
    {expr} param_assignment_lvalue? expression |
    {arrow} not_token? identifier arrow_token variable;

  // B.3.2.3 Selection statements

  selection_statement =
    {if} if_statement |
    {case} case_statement;

  if_statement =
    if_token expression then_token statement_list elseif_statement* else_statement* end_if_token;

  elseif_statement =
    else_if_token expression then_token statement_list;

  else_statement =
    else_token statement_list;

  case_statement_else_clause =
    else_token statement_list;

  case_statement =
    case_token expression of_token case_element+ case_statement_else_clause? end_case_token;

  case_element =
    case_list colon statement_list;

  case_list =
    case_list_head* case_list_element;

  case_list_head =
    case_list_element comma;

  case_list_element =
    {subrange} subrange |
    {integer} integer_literal;
    //enumerated_value;

  // B.3.2.4 Iteration statements

  iteration_statement =
    {for} for_statement |
    {while} while_statement |
    {repeat} repeat_statement |
    {exit} exit_statement;

  for_statement =
    for_token control_variable colon_eq for_list do_token statement_list end_for_token;

  control_variable =
    identifier;

  for_list_by_clause =
    by_token [by]:expression;

  for_list =
    [from]:expression to_token [to]:expression for_list_by_clause?;

  while_statement =
    while_token expression do_token statement_list end_while_token;

  repeat_statement =
    repeat_token statement_list until_token expression end_repeat_token;

  exit_statement =
    exit_token;
